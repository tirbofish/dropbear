// Main shader for standard objects.

const MAX_LIGHTS: u32 = 8;

struct CameraUniform {
    view_pos: vec4<f32>,
    view_proj: mat4x4<f32>,
};

struct Light {
    position: vec4<f32>,
    direction: vec4<f32>, // x, y, z, outer_cutoff_angle
    color: vec4<f32>, // r, g, b, light_type (0, 1, 2)
    constant: f32,
    lin: f32,
    quadratic: f32,
    cutoff: f32,
    shadow_index: i32,
    proj: mat4x4<f32>,
}

struct LightArray {
    _lights: array<Light, MAX_LIGHTS>,
    light_count: u32,
    ambient_strength: f32,
}

@group(0) @binding(0)
var t_diffuse: texture_2d<f32>;
@group(0) @binding(1)
var s_diffuse: sampler;

@group(1) @binding(0)
var<uniform> camera: CameraUniform;

@group(2) @binding(0)
var<uniform> light_array: LightArray;
@group(2) @binding(1)
var t_shadow: texture_depth_2d_array;
@group(2) @binding(2)
var s_shadow: sampler_comparison;

struct InstanceInput {
    @location(5) model_matrix_0: vec4<f32>,
    @location(6) model_matrix_1: vec4<f32>,
    @location(7) model_matrix_2: vec4<f32>,
    @location(8) model_matrix_3: vec4<f32>,

    @location(9) normal_matrix_0: vec3<f32>,
    @location(10) normal_matrix_1: vec3<f32>,
    @location(11) normal_matrix_2: vec3<f32>,
};

struct VertexInput {
    @location(0) position: vec3<f32>,
    @location(1) tex_coords: vec2<f32>,
    @location(2) normal: vec3<f32>,
};

struct VertexOutput {
    @builtin(position) clip_position: vec4<f32>,
    @location(0) tex_coords: vec2<f32>,
    @location(1) world_normal: vec3<f32>,
    @location(2) world_position: vec3<f32>,
};

@vertex
fn vs_main(
    model: VertexInput,
    instance: InstanceInput,
) -> VertexOutput {
    let model_matrix = mat4x4<f32>(
        instance.model_matrix_0,
        instance.model_matrix_1,
        instance.model_matrix_2,
        instance.model_matrix_3,
    );
    let normal_matrix = mat3x3<f32>(
        instance.normal_matrix_0,
        instance.normal_matrix_1,
        instance.normal_matrix_2,
    );
    var out: VertexOutput;
    out.tex_coords = model.tex_coords;
    out.world_normal = normal_matrix * model.normal;
    var world_position: vec4<f32> = model_matrix * vec4<f32>(model.position, 1.0);
    out.world_position = world_position.xyz;
    out.clip_position = camera.view_proj * world_position;
    return out;
}

fn calculate_light(light: Light, world_pos: vec3<f32>, world_normal: vec3<f32>, view_dir: vec3<f32>) -> vec3<f32> {
    let light_dir = normalize(light.position.xyz - world_pos);
    
    // dihfuse
    let diffuse_strength = max(dot(world_normal, light_dir), 0.0);
    let diffuse_color = light.color.xyz * diffuse_strength;
    
    // specular
    let half_dir = normalize(view_dir + light_dir);
    let specular_strength = pow(max(dot(world_normal, half_dir), 0.0), 32.0);
    let specular_color = specular_strength * light.color.xyz;
    
    return diffuse_color + specular_color;
}

fn calculate_shadow(light: Light, world_pos: vec3<f32>, normal: vec3<f32>, light_dir: vec3<f32>) -> f32 {
    // if -1, it means it doesnt cast shadows
    if (light.shadow_index < 0) {
        return 1.0;
    }

    let light_space_pos = light.proj * vec4<f32>(world_pos, 1.0);

    if (light_space_pos.w <= 0.0) {
        return 1.0;
    }
    let proj_correction = 1.0 / light_space_pos.w;

    let flip_correction = vec2<f32>(0.5, -0.5);
    let light_local = light_space_pos.xy * flip_correction * proj_correction + vec2<f32>(0.5, 0.5);

    let current_depth = light_space_pos.z * proj_correction;

    if (light_local.x < 0.0 || light_local.x > 1.0 || light_local.y < 0.0 || light_local.y > 1.0 || current_depth > 1.0) {
        return 1.0;
    }

    // 5. Sample Texture Array
    // We use the light.shadow_index to pick the specific layer
    // Note: Applying a small bias to 'current_depth' prevents shadow acne.
    // However, wgpu example sets bias in pipeline state. If you see acne, subtract 0.005 here.
    return textureSampleCompare(
        t_shadow,
        s_shadow,
        light_local,
        light.shadow_index,
        current_depth - 0.002 // Small bias
    );
}

fn directional_light(
    light: Light,
    world_normal: vec3<f32>,
    view_dir: vec3<f32>,
    tex_color: vec3<f32>,
    world_pos: vec3<f32>
) -> vec3<f32> {
    let light_dir = normalize(-light.direction.xyz);

    let ambient = light.color.xyz * light_array.ambient_strength * tex_color;

    let shadow = calculate_shadow(light, world_pos, world_normal, light_dir);

    let diff = max(dot(world_normal, light_dir), 0.0);
    let diffuse = light.color.xyz * diff * tex_color;

    let reflect_dir = reflect(-light_dir, world_normal);
    let spec = pow(max(dot(view_dir, reflect_dir), 0.0), 32.0);
    let specular = light.color.xyz * spec * tex_color;

    return ambient + (shadow * (diffuse + specular));
}

// https://learnopengl.com/code_viewer_gh.php?code=src/2.lighting/5.2.light_casters_point/5.2.light_casters.fs
// deal with later. current issue: it is showing only yellow and white in point light (weird...)
// note: fixed, forgot to push attenuation values to gpu lol
fn point_light(
    light: Light,
    world_pos: vec3<f32>,
    world_normal: vec3<f32>,
    view_dir: vec3<f32>,
    tex_color: vec3<f32>
) -> vec3<f32> {
    let norm = normalize(world_normal);
    let light_dir = normalize(light.position.xyz - world_pos);

    let shadow = calculate_shadow(light, world_pos, world_normal, light_dir);

    let diff = max(dot(norm, light_dir), 0.0);
    let diffuse = light.color.xyz * diff * tex_color;

    let shininess = 32.0;
    let reflect_dir = reflect(-light_dir, norm);
    let spec = pow(max(dot(view_dir, reflect_dir), 0.0), shininess);
    let specular = light.color.xyz * spec * tex_color;

    let distance = length(light.position.xyz - world_pos);
    let attenuation = 1.0 / (light.constant + (light.lin * distance) + (light.quadratic * (distance * distance)));

    return (shadow * (diffuse + specular)) * attenuation;
}

fn spot_light(
    light: Light,
    world_pos: vec3<f32>,
    world_normal: vec3<f32>,
    view_dir: vec3<f32>,
    tex_color: vec3<f32>
) -> vec3<f32> {
    let outer_cutoff = light.direction.w;
    let ambient = light.color.xyz * light_array.ambient_strength * tex_color;

    let norm = normalize(world_normal);
    let light_dir = normalize(light.position.xyz - world_pos);

    let shadow = calculate_shadow(light, world_pos, world_normal, light_dir);

    let diff = max(dot(norm, light_dir), 0.0);
    var diffuse = light.color.xyz * diff * tex_color;

    let shininess = 32.0;
    let reflect_dir = reflect(-light_dir, norm);
    let spec = pow(max(dot(view_dir, reflect_dir), 0.0), shininess);
    var specular = light.color.xyz * spec * tex_color;

    let theta = dot(light_dir, normalize(-light.direction.xyz));
    let epsilon = light.cutoff - outer_cutoff;
    let intensity = clamp((theta - outer_cutoff) / epsilon, 0.0, 1.0);

    diffuse *= intensity;
    specular *= intensity;

    let distance = length(light.position.xyz - world_pos);
    let attenuation = 1.0 / (light.constant + (light.lin * distance) + (light.quadratic * (distance * distance)));

    let ambient_attenuated = ambient * attenuation;
    let diffuse_attenuated = diffuse * attenuation;
    let specular_attenuated = specular * attenuation;

    return ambient_attenuated + (shadow * (diffuse_attenuated + specular_attenuated));
}

@fragment
fn fs_main(in: VertexOutput) -> @location(0) vec4<f32> {
    var tex_color = textureSample(t_diffuse, s_diffuse, in.tex_coords);
    if (tex_color.a < 0.1) {
        discard;
    }

    let view_dir = normalize(camera.view_pos.xyz - in.world_position);
    let world_normal = normalize(in.world_normal);

    var final_color = vec3<f32>(0.0);

    var total_ambient = vec3<f32>(0.0);
    for (var i: u32 = 0u; i < light_array.light_count; i = i + 1u) {
        let light = light_array._lights[i];
        total_ambient += light.color.xyz * light_array.ambient_strength;
    }

    for (var i: u32 = 0u; i < light_array.light_count; i = i + 1u) {
        let light = light_array._lights[i];

        // light type is color.w
        if light.color.w == 0.0 {
            // dir
            final_color += directional_light(light, world_normal, view_dir, tex_color.xyz, in.world_position);
        } else if light.color.w == 1.0 {
            // point
            final_color += point_light(light, in.world_position, world_normal, view_dir, tex_color.xyz);
        } else if light.color.w == 2.0 {
            // spot
            final_color += spot_light(light, in.world_position, world_normal, view_dir, tex_color.xyz);
        }
    }

//    final_color = (total_ambient * tex_color.xyz) + final_color;

    return vec4<f32>(final_color, tex_color.a);
}